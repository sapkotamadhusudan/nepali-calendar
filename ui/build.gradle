plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

ext {
    PUBLISH_GROUP_ID = 'io.github.sapkotamadhusudan'
    PUBLISH_VERSION = '1.0.0'
    PUBLISH_ARTIFACT_ID = 'np-calendar-ui'
    PUBLISH_DESCRIPTION = 'A Nepali Calendar used to manage nepali dates and also convert AD dates to nepali.'
}
apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName PUBLISH_VERSION
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true // Because library's minSdk < 21
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-module-name', "${PUBLISH_GROUP_ID}.${PUBLISH_ARTIFACT_ID}"]
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
            // Show the result of every unit test, even if it passes.
            all {
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
        }
    }

    sourceSets {
        all {
            it.java.srcDir "src/$it.name/kotlin"
        }
    }
}


dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0-native-mt"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3"


    // Expose RecyclerView which is CalendarView's superclass to
    // prevent a compile error when using the library in a project:
    // Error: Supertypes of the following classes cannot be resolved.
    // Please make sure you have the required dependencies in the classpath:
    // class com.maddy.calendar.ui.CalendarView,
    // unresolved supertypes: androidx.recyclerview.widget.RecyclerView

    // Not exposing RecyclerView also means that calling findViewById<CalendarView>
    // results in an error: Type argument is not within its bounds, Expected: View.
    // This is because CalendarView's superclass type cannot be determined.
    api 'androidx.recyclerview:recyclerview:1.2.1'

    api 'io.github.sapkotamadhusudan:np-calendar-core:1.0.0'

    testImplementation "junit:junit:4.13.2"
}
