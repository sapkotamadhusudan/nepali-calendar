plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.bintray' version "1.8.1"
    id 'maven-publish'
}

def getVersionName = { ->
    return "0.0.5"
}

def getDescription = { ->
    return "A Nepali Calendar used to manage nepali dates and also convert AD dates to nepali."
}

def getVCSUrl = { ->
    return "https://github.com/sapkotamadhusudan/nepali-calendar"
}

def getLicense = { ->
    return ["MIT"]
}

def getArtifactId = { ->
    return "ui"
}

def getGroupId = { ->
    return "com.maddy.calendar"
}

def getPackageName = {->
    return getGroupId() + ":" + getArtifactId()
}

afterEvaluate {
    publishing {
        publications {
            uiRelease(MavenPublication) {
                from components.release

                groupId getGroupId()
                artifactId getArtifactId()
                version getVersionName()
                // Place the path of your artifact here
                artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")
            }
        }
    }
}

// Bintray config
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ["uiRelease"]
    pkg {
        repo = "nepali-calendar"
        name = getPackageName()
        desc = getDescription()
        licenses = getLicense()
        vcsUrl = getVCSUrl()
        version {
            name = getVersionName()
            vcsTag = getVersionName()
            released = new Date()
        }
    }
}

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true // Because library's minSdk < 21
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-module-name', "${getGroupId()}.${getArtifactId()}"]
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"


    // Expose RecyclerView which is CalendarView's superclass to
    // prevent a compile error when using the library in a project:
    // Error: Supertypes of the following classes cannot be resolved.
    // Please make sure you have the required dependencies in the classpath:
    // class com.maddy.calendar.ui.CalendarView,
    // unresolved supertypes: androidx.recyclerview.widget.RecyclerView

    // Not exposing RecyclerView also means that calling findViewById<CalendarView>
    // results in an error: Type argument is not within its bounds, Expected: View.
    // This is because CalendarView's superclass type cannot be determined.
    api 'androidx.recyclerview:recyclerview:1.2.0-beta01'

    api project(path: ':core')

    testImplementation "junit:junit:4.13.1"
}

